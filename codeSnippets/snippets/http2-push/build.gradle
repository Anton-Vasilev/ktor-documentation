buildscript {
    ext.tcnative_version = '2.0.40.Final'

    // Determine native library that we need for Netty SSL / HTTP2 support

    def osName = System.getProperty('os.name').toLowerCase()

    if (osName.contains('win')) {
        ext.tcnative_classifier = 'windows-x86_64'
    } else if (osName.contains('linux')) {
        ext.tcnative_classifier = 'linux-x86_64'
    } else if (osName.contains('mac')) {
        ext.tcnative_classifier = 'osx-x86_64'
    } else {
        ext.tcnative_classifier = ''
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "io.ktor.snippets.http2push.MainKt"

sourceSets {
    main.kotlin.srcDirs = [ 'src' ]
    main.resources.srcDirs = [ 'resources' ]
}

repositories {
    mavenCentral()
    maven { url "https://maven.pkg.jetbrains.space/public/p/ktor/eap" }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-html-builder:$ktor_version"
    implementation "io.ktor:ktor-server-default-headers:$ktor_version"
    implementation "io.ktor:ktor-server-call-logging:$ktor_version"
    implementation "io.ktor:ktor-network-tls:$ktor_version"
    implementation "io.ktor:ktor-network-tls-certificates:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.netty:netty-tcnative:$tcnative_version"
    implementation "io.netty:netty-tcnative-boringssl-static:$tcnative_version"
    implementation "io.netty:netty-tcnative-boringssl-static:$tcnative_version:$tcnative_classifier"
}

